LDAP Search Filters

The formal definition of the search filter is as follows (from RFC 1960):

<filterlist> ::= <filter> | <filter> <filterlist>
<filter> ::= '(' <filtercomp> ')'
<filtercomp> ::= <and> | <or> | <not> | <item>

<and> ::= '&' <filterlist>
<or> ::= '|' <filterlist>
<not> ::= '!' <filter>

<item> ::= <simple> | <present> | <substring>

<simple> ::= <attr> <filtertype> <value> 

<filtertype> ::= <equal> | <approx> | <ge> | <le>

<equal> ::= '='
<approx> ::= '~='
<ge> ::= '>='
<le> ::= '<='

<present> ::= <attr> '=*'

<substring> ::= <attr> '=' <initial> <any> <final>

<initial> ::= NULL | <value>
<any> ::= '*' <starval>
<starval> ::= NULL | <value> '*' <starval>
<final> ::= NULL | <value>

The token <attr> is a string that represents an AttributeType.

The token <value> is a string that represents an AttributeValue whose format is defined by the underlying directory service. If a <value> must contain the asterisk (*), left parenthesis ((), or right parenthesis ()) character, the character should be preceded by the backslash escape character (\).
